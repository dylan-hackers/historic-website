<?xml version="1.0"?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
        "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
<link title="main style sheet" rel="stylesheet" href="dpg.css" type="text/css" />
<link rel="top" href="db_1.html" />
<link rel="up" href="db_268.html" />
<link rel="prev" href="db_268.html" />
<link rel="next" href="db_270.html" />
<title>17.4.1   The container protocol and implementation</title>
</head>
<body>

<div>
<a name="heading269"></a>

<div>
<p><a href="db_270.html"><img src="next.gif" alt="next" /></a> <a href="db_268.html"><img src="prev.gif" alt="previous" /></a> <a href="db_1.html"><img src="top.gif" alt="top" /></a> <a href="db_2.html"><img src="content.gif" alt="contents" /></a> <a href="db_349.html"><img src="index.gif" alt="index" /></a></p>

</div>
<a name="heading269-0"></a>
<h1>17.4.1   The container protocol and implementation</h1>
<p> </p>
<table border="1"><tr valign="top"><th><p> The <code>schedule.dylan</code> file.  </p>
</th></tr><tr valign="top"><td><pre>
module: airport
<em>// The following generic functions constitute the essential protocol for
// interaction between containers and vehicles</em>
<em>// Returns true if container is available for aircraft in direction
</em>define generic available? (vehicle, container, direction); 
</pre></td></tr><tr valign="top"><td><em>// Moves vehicle into container in the given direction
</em>define generic move-in-vehicle (vehicle, container, direction); 
</td></tr><tr valign="top"><td><em>// Moves vehicle out of container in the given direction
</em>define generic move-out-vehicle (vehicle, container, direction); 
</td></tr><tr valign="top"><td><em>// Returns the aircraft next in line to move out of container in direction
</em>define generic next-out (container, direction); 
</td></tr><tr valign="top"><td><em>// Returns the class of the next container to move vehicle into, 
// and how long it will take to get there
</em>define generic next-landing-step (container, vehicle); 
</td></tr><tr valign="top"><td><em>// A single storage container is available if the aircraft fits into the
// the container, and there is not already a vehicle in the container
</em>define method available?
    (vehicle :: &lt;aircraft&gt;, container :: &lt;single-storage&gt;,
     direction :: &lt;symbol&gt;)
 =&gt; (container-available? :: &lt;boolean&gt;)
  object-fits?(vehicle, container)
  &amp; ~ (container.vehicle-currently-occupying);
end method available?; 
</td></tr><tr valign="top"><td><em>// A multiple storage container is available if the aircraft fits into 
// the container, and there are not too many aircraft already queued in
// the container for the specified direction
</em>define method available? 
    (vehicle :: &lt;aircraft&gt;, container :: &lt;multiple-storage&gt;,
     direction :: &lt;symbol&gt;)
 =&gt; (container-available? :: &lt;boolean&gt;)
  object-fits?(vehicle, container)
  &amp; size(container.vehicles-by-direction[direction])
    &lt; container.maxima-by-direction[direction];
end method available?; 
</td></tr><tr valign="top"><td><em>// Avoids jamming the runway with inbound traffic, which would prevent
// outbound aircraft from taking off
// The runway is clear to inbound traffic only if there is space in the 
// next container inbound from the runway
</em>define method available?
    (vehicle :: &lt;aircraft&gt;, container :: &lt;runway&gt;,
     direction :: &lt;symbol&gt;) 
 =&gt; (container-available? :: &lt;boolean&gt;)
  next-method()
    &amp; select (direction)
        #&quot;outbound&quot; =&gt; #t;
        #&quot;inbound&quot;
          =&gt; let (class) = next-landing-step(container, vehicle);
             if (class)
               find-available-connection(container, class, vehicle) ~== #f;
             end if;
      end select;
end method available?; 
</td></tr><tr valign="top"><td><em>// A slot is used to keep track of which aircraft is in a single 
// storage container
</em>define method move-in-vehicle
    (vehicle :: &lt;aircraft&gt;, container :: &lt;single-storage&gt;,
     direction :: &lt;symbol&gt;)
 =&gt; ()
  container.vehicle-currently-occupying := vehicle;
  values();
end method move-in-vehicle; 
</td></tr><tr valign="top"><td><em>// A deque is used to keep track of which aircraft are traveling in a
// particular direction in a multiple storage container
</em>define method move-in-vehicle
    (vehicle :: &lt;aircraft&gt;, container :: &lt;multiple-storage&gt;,
     direction :: &lt;symbol&gt;)
 =&gt; ()
  let vehicles = container.vehicles-by-direction[direction];
  push-last(vehicles, vehicle);
  values();
end method move-in-vehicle; 
</td></tr><tr valign="top"><td><em>// When an aircraft reaches the gate, it begins its outbound journey
</em>define method move-in-vehicle
    (vehicle :: &lt;aircraft&gt;, container :: &lt;gate&gt;, 
     direction :: &lt;symbol&gt;)
 =&gt; ()
  next-method();
  vehicle.direction := #&quot;outbound&quot;;
  values();
end method move-in-vehicle; 
</td></tr><tr valign="top"><td>define method move-out-vehicle
    (vehicle :: &lt;aircraft&gt;, container :: &lt;single-storage&gt;,
     direction :: &lt;symbol&gt;)
 =&gt; ()
  container.vehicle-currently-occupying := #f;
  values();
end method move-out-vehicle; 
</td></tr><tr valign="top"><td>define method move-out-vehicle
    (vehicle   :: &lt;aircraft&gt;,
     container :: &lt;multiple-storage&gt;, direction :: &lt;symbol&gt;)
 =&gt; ()
  let vehicles = container.vehicles-by-direction[direction];
<em>  // Assumes that aircraft always exit container in order, and 
  // that this aircraft is next
</em>  pop(vehicles);
  values();
end method move-out-vehicle; 
</td></tr><tr valign="top"><td><em>// Determines what vehicle, if any, could move to the next container
// If there is such a vehicle, then this method returns the vehicle, 
// the next container in the direction of travel, 
// and the time that it would take to make that transition
</em>define method next-out
    (container :: &lt;vehicle-storage&gt;, direction :: &lt;symbol&gt;)
 =&gt; (next-vehicle :: false-or(&lt;vehicle&gt;),
     next-storage :: false-or(&lt;vehicle-storage&gt;),
     time-to-execute :: false-or(&lt;time-offset&gt;));
  let next-vehicle = next-out-internal(container, direction);
  if (next-vehicle)
    let (class, time) = next-landing-step(container, next-vehicle);
    if (class)
      let next-container
        = find-available-connection(container, class, next-vehicle);
      if (next-container)
        values(next-vehicle, next-container, time);
      end if;
    end if;
  end if;
end method next-out; 
</td></tr><tr valign="top"><td><em>// This method is just a helper method for the next-out method
// We need different methods based on the class of container
</em>define method next-out-internal
    (container :: &lt;single-storage&gt;, desired-direction :: &lt;symbol&gt;)
 =&gt; (vehicle :: false-or(&lt;aircraft&gt;))
  let vehicle = container.vehicle-currently-occupying;
  if (vehicle &amp; vehicle.direction == desired-direction) vehicle; end;
end method next-out-internal; 
</td></tr><tr valign="top"><td>define method next-out-internal
    (container :: &lt;multiple-storage&gt;, desired-direction :: &lt;symbol&gt;)
 =&gt; (vehicle :: false-or(&lt;aircraft&gt;))
  let vehicle-queue = container.vehicles-by-direction[desired-direction];
  if (vehicle-queue.size &gt; 0) vehicle-queue[0]; end;
end method next-out-internal; 
</td></tr><tr valign="top"><td><em>// The following methods return the class of the next container to which a
// vehicle can move from a particular container
// They also return an estimate of how long that transition will take
</em>define method next-landing-step
    (storage :: &lt;sky&gt;, aircraft :: &lt;aircraft&gt;)
 =&gt; (next-class :: false-or(&lt;class&gt;), duration :: false-or(&lt;time-offset&gt;))
  if (aircraft.direction == #&quot;inbound&quot;)
    values(&lt;runway&gt;, flying-time(aircraft, storage.airport-below));
  end if;
end method next-landing-step; 
</td></tr><tr valign="top"><td>define method next-landing-step
    (storage :: &lt;runway&gt;, aircraft :: &lt;aircraft&gt;)
 =&gt; (next-class :: &lt;class&gt;, duration :: &lt;time-offset&gt;)
  select (aircraft.direction)
    #&quot;inbound&quot;  =&gt; values(&lt;taxiway&gt;, brake-time(aircraft, storage));
    #&quot;outbound&quot; =&gt; values(&lt;sky&gt;, takeoff-time(aircraft, storage));
  end select;
end method next-landing-step; 
</td></tr><tr valign="top"><td>define method next-landing-step
    (storage :: &lt;taxiway&gt;, aircraft :: &lt;aircraft&gt;)
 =&gt; (next-class :: &lt;class&gt;, duration :: &lt;time-offset&gt;)
  select (aircraft.direction)
    #&quot;inbound&quot;  =&gt; values(&lt;gate&gt;, gate-time(aircraft, storage));
    #&quot;outbound&quot; =&gt; values(&lt;runway&gt;, runway-time(aircraft, storage));
  end select;
end method next-landing-step; 
</td></tr><tr valign="top"><td><pre>define method next-landing-step
    (storage :: &lt;gate&gt;, aircraft :: &lt;aircraft&gt;)
 =&gt; (next-class :: &lt;class&gt;, duration :: &lt;time-offset&gt;)
  values(&lt;taxiway&gt;, gate-turnaround(aircraft, storage));
end method next-landing-step; 
</pre></td></tr></table>
<p> </p>

</div>

<div>

<div>

<!-- TOC -->

</div>

<hr />
<address>Dylan Programming - 9 Apr 1999</address>
<p><a href="db_270.html"><img src="next.gif" alt="next" /></a> <a href="db_268.html"><img src="prev.gif" alt="previous" /></a> <a href="db_1.html"><img src="top.gif" alt="top" /></a> <a href="db_2.html"><img src="content.gif" alt="contents" /></a> <a href="db_349.html"><img src="index.gif" alt="index" /></a></p>
<p>N Feinberg/S E Keene/R Mathews/P Tucker Withington, DYLAN PROGRAMMING, (c) 1997 Harlequin Inc. Reproduced by permission of Addison-Wesley Longman Publishing Company, Inc.  All rights reserved. No further copying, downloading or transmitting of this material is allowed without the prior written permission of the publisher.</p>

</div>
</body>
</html>
