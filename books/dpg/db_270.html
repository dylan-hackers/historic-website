<?xml version="1.0"?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
        "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
	<head>
		<link title="main style sheet" rel="stylesheet" href="dpg.css" type="text/css" />
		<link rel="top" href="db_1.html" />
		<link rel="up" href="db_268.html" />
		<link rel="prev" href="db_269.html" />
		<link rel="next" href="db_271.html" />
		<title>Dylan Programming: 17.4.2   The scheduling algorithm</title>
	</head>
<body>

<div class="navigation">
	<p><a href="db_271.html"><img src="next.gif" alt="next" /></a> <a href="db_269.html"><img src="prev.gif" alt="previous" /></a> <a href="db_1.html"><img src="top.gif" alt="top" /></a> <a href="db_2.html"><img src="content.gif" alt="contents" /></a> <a href="db_349.html"><img src="index.gif" alt="index" /></a></p>
</div>

<div class="content" id="heading270-0">
<h1>17.4.2   The scheduling algorithm</h1>
<p>The next methods form the core of the airport application.</p>
<table><tr><th><p>The <code>schedule.dylan</code> file.<em> (continued)</em></p>
</th></tr><tr><td><pre>
<em>// Searches all of the vehicle storage of class class-of-next, which is
// connected to container and has room for aircraft
</em>define method find-available-connection
    (storage :: &lt;vehicle-storage&gt;, class-of-next :: &lt;class&gt;,
     aircraft :: &lt;aircraft&gt;)
 =&gt; (next-container :: false-or(&lt;vehicle-storage&gt;))
  block (return)
    for (c in storage.connected-to)
      if (instance?(c, class-of-next)
          &amp; available?(aircraft, c, aircraft.direction))
        return(c);
      end if;
    end for;
  end block;
end method find-available-connection; 

<em>// Generate new transitions to be considered for the next move
// The transitions will be placed in the sorted sequence, which will order
// them by earliest arrival time
</em>define method generate-new-transitions
    (container :: &lt;vehicle-storage&gt;, 
     active-transitions :: &lt;sorted-sequence&gt;,
     containers-visited :: &lt;object-table&gt;)
 =&gt; ()
  unless(element(containers-visited, container, default: #f))
<em>    // Keep track of which containers we have searched for new possible
    // transitions
    // We avoid looping forever by checking each container just once
</em>    containers-visited[container] := #t; 

    local method consider-transition (direction)
<em>      // See whether any vehicle is ready to transition out of a container
</em>      let (vehicle, next-container, time) 
        = next-out(container, direction);
      unless (vehicle == #f | vehicle.next-transition.pending?)
<em>        // If there is a vehicle ready, and it is not already in the 
        // sorted sequence of pending transitions, then prepare the 
        // transition instance associated with the vehicle 
</em>        let transition = vehicle.next-transition;
        transition.from-container := container;
        transition.to-container := next-container;

<em>        // The vehicle may have been waiting
        // Take this situation into account when computing the earliest
        // arrival into the next container
</em>        transition.earliest-arrival := transition.earliest-arrival + time;
<em>        // Flag the vehicle as pending, to save searching through the
        // active-transitions sorted sequence later
</em>        transition.pending? := #t;
<em>        // Add the transition to the set to be considered 
</em>        add!(active-transitions, transition);
      end unless;
    end method consider-transition; 

<em>    // Consider both inbound and outbound traffic 
</em>    consider-transition(#&quot;outbound&quot;);
    consider-transition(#&quot;inbound&quot;);
<em>    // Make sure that every container connected to this one is checked
</em>    for (c in container.connected-to)
      generate-new-transitions(c, active-transitions, containers-visited);
    end for;
  end unless;
end method generate-new-transitions; 

<em>// Main loop of the program
// See what possible transitions exist, then execute the earliest
// transitions that can be completed
// Returns the time of the last transition
</em>define method process-aircraft 
    (airport :: &lt;airport&gt;, #key time = $midnight) 
 =&gt; (time :: &lt;time-of-day&gt;)
  format-out(&quot;Detailed aircraft schedule for &quot;);
  say(airport);
  format-out(&quot;\n\n&quot;);
  let sky = airport.sky-above;
  let containers-visited = make(&lt;object-table&gt;);
  let active-transitions = make(&lt;sorted-sequence&gt;, 
                               value-function: earliest-arrival); 

<em>  // We do not have to use return as the name of the exit procedure
</em>  block (done)
    while (#t)
<em>      // Each time through, start by considering every container
</em>      fill!(containers-visited, #f);
<em>      // For every container, see if any vehicles are ready to transition
      // If any are, add transition instances to the active-transitions 
      // sorted sequence
</em>      generate-new-transitions(sky, active-transitions, 
                               containers-visited);

<em>      // If there are no more transitions, we have completed our task
</em>      if (empty?(active-transitions)) done(); end;
<em>      // Find the earliest transition that can complete, because there is 
      // still room available in the destination container
</em>      let transition-index
        = find-key(active-transitions,
                   method (transition)
                     available?(transition.transition-aircraft,
                                transition.to-container,
                                transition.transition-aircraft.direction);
                   end); 

<em>      // If none can complete, there is a problem with the simulation
      // This situation should never occur, but is useful for debugging
      // incorrect container configurations
</em>      if (transition-index == #f)
        error(&quot;Pending transitions but none can complete.&quot;);
      end if; 

<em>      // Otherwise, the earliest transition that can complete has been
      // found: Execute the transition
</em>      let transition = active-transitions[transition-index];
      let vehicle = transition.transition-aircraft;
      let vehicle-direction = vehicle.direction;
      move-out-vehicle(vehicle, transition.from-container, 
                       vehicle-direction);
      move-in-vehicle(vehicle, transition.to-container, vehicle-direction); 
</pre>
<pre><em>      // This transition is complete; remove it from consideration 
</em>      transition.pending? := #f;
      remove!(active-transitions, transition);
<em>      // Compute the actual time of arrival at the next container, and
       // display the message 
</em>      time := (transition.earliest-arrival 
                := max(time, transition.earliest-arrival));
      say(transition);
      format-out(&quot;\n&quot;);
    end while;
  end block;
  time;
end method process-aircraft; 
</pre></td></tr></table>
<p>The <code>process-aircraft</code> method uses components from the time, space and sorted sequence libraries, the container classes and protocols, and the vehicle classes and methods to schedule the aircraft arriving and departing from an airport. The <code>generate-new-transitions</code> method assists by examining the current state of all containers in the airport, and by noting any new steps that vehicles could take.</p>

</div>

<div class="toc">


</div>

<div class="footer">
<address>Dylan Programming - 9 Apr 1999</address>
</div>
<div class="navigation">
	<p><a href="db_271.html"><img src="next.gif" alt="next" /></a> <a href="db_269.html"><img src="prev.gif" alt="previous" /></a> <a href="db_1.html"><img src="top.gif" alt="top" /></a> <a href="db_2.html"><img src="content.gif" alt="contents" /></a> <a href="db_349.html"><img src="index.gif" alt="index" /></a></p>
</div>
<div class="copyright">
	<p>N Feinberg/S E Keene/R Mathews/P Tucker Withington, DYLAN PROGRAMMING, (c) 1997 Harlequin Inc. Reproduced by permission of Addison-Wesley Longman Publishing Company, Inc.  All rights reserved. No further copying, downloading or transmitting of this material is allowed without the prior written permission of the publisher.</p>
</div>
</body>
</html>
