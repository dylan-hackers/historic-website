<?xml version="1.0"?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
        "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
	<head>
		<link title="main style sheet" rel="stylesheet" href="dpg.css" type="text/css" />
		<link rel="top" href="db_1.html" />
		<link rel="up" href="db_260.html" />
		<link rel="prev" href="db_270.html" />
		<link rel="next" href="db_272.html" />
		<title>Dylan Programming: 17.5   The airport-test.dylan file</title>
	</head>
<body>

<div class="navigation">
	<p><a href="db_272.html" accesskey="n"><img src="next.gif" alt="next" /></a> <a href="db_270.html" accesskey="p"><img src="prev.gif" alt="previous" /></a> <a href="db_1.html" accesskey="t"><img src="top.gif" alt="top" /></a> <a href="db_2.html" accesskey="c"><img src="content.gif" alt="contents" /></a> <a href="db_349.html" accesskey="i"><img src="index.gif" alt="index" /></a></p>
</div>

<div class="content" id="heading271-0">
<h1>17.5   <a name="marker-9-712"></a>The <code>airport-test.dylan</code> file</h1>
<p>The <code>airport-test.dylan</code> file contains test data, and the code that constructs a model of the simple airport described in <a href="db_244.html#marker-9-681">Section 15.1, page 237</a>. The final method is a top-level testing function that builds the airport model and executes the main aircraft scheduling function. After defining the test, we show the results of running it. </p>
<table><tr><th><p>The <code>airport-test.dylan</code> file.  </p>
</th></tr><tr><td><pre>
module: airport-test
<em>// To keep the example relatively simple, we will use variables to hold
// test data for the flights and aircraft 
// Ordinarily, this information would be read from a file or database </em>
define variable *flight-numbers* = #[62, 7, 29, 12, 18, 44]; 

define variable *aircraft-distances* 
  = #[3.0, 10.0, 175.0, 450.0, 475.0, 477.0];       <em>// Miles</em>

define variable *aircraft-headings* 
  = #[82, 191, 49, 112, 27, 269];       <em>// Degrees </em>

define variable *aircraft-altitudes*
  = #[7000, 15000, 22000, 22500, 22000, 21000];    <em>// Feet </em>

define variable *aircraft-ids*
  = #[&quot;72914&quot;, &quot;82290&quot;, &quot;18317&quot;, &quot;26630&quot;, &quot;43651&quot;, &quot;40819&quot;]; 

define constant $default-runway-size
  = make(&lt;size&gt;, length: 10000, width: 200, height: 100);    <em>// Feet </em>

define constant $default-taxiway-size
  = make(&lt;size&gt;, length: 900, width: 200, height: 100);    <em>// Feet </em>

<em><a name="marker-9-703"></a>// Assumes that there is only one runway, and one taxiway 
// The taxiway-count variable will determine how many aircraft can wait 
// in line for each direction of the taxiway
</em>define method build-simple-airport
    (#key gates-per-terminal :: &lt;vector&gt; = #[2],
     capacity :: &lt;size&gt; = $default-capacity,
     runway-size :: &lt;size&gt; = $default-runway-size,
     taxiway-size :: &lt;size&gt; = $default-taxiway-size,
     taxiway-count :: &lt;positive-integer&gt; = 5,
     position-report-time :: &lt;time-of-day&gt;
       = make(&lt;time-of-day&gt;, 
           total-seconds: encode-total-seconds(6, 0, 0)))
 =&gt; (airport :: &lt;airport&gt;) 

  let gates = generate-gates(gates-per-terminal, capacity);
  let taxiway
    = make(&lt;taxiway&gt;, id: &quot;Echo&quot;, directions: #[#&quot;inbound&quot;, #&quot;outbound&quot;],
           maxima: vector(taxiway-count, taxiway-count),
           capacity: capacity, physical-size: taxiway-size);
  let runway = make(&lt;runway&gt;, id: &quot;11R-29L&quot;, capacity: capacity,
                    physical-size: runway-size);
  let keystone-air = make(&lt;airline&gt;, name: &quot;Keystone Air&quot;, code: &quot;KN&quot;);
  let flights
    = map(method (fn) 
          make(&lt;flight&gt;, airline: keystone-air, number: fn) end,
          *flight-numbers*); 

  let aircraft
    = map(method (aircraft-flight, aircraft-distance, aircraft-heading,
                  aircraft-altitude, aircraft-id)
            make(&lt;b707&gt;, 
                 flight: aircraft-flight,
                 current-position:
                   make(&lt;relative-position&gt;,
                        distance: aircraft-distance,
                        angle: 
                          make(&lt;relative-angle&gt;,
                               total-seconds: 
                                 encode-total-seconds
                                   (aircraft-heading, 0, 0))),
                 altitude: aircraft-altitude,
                 id: aircraft-id,
                 transition: make(&lt;aircraft-transition&gt;,
                                  arrival: position-report-time));
          end,

          flights, *aircraft-distances*, *aircraft-headings*,
          *aircraft-altitudes*, *aircraft-ids*); 

  let airport
    = make(&lt;airport&gt;,
           name: &quot;Belefonte Airport&quot;,
           code: &quot;BLA&quot;,
           current-position:
             make(&lt;absolute-position&gt;,
                  latitude:
                    make(&lt;latitude&gt;,
                         total-seconds: encode-total-seconds(40, 57, 43),
                         direction: #&quot;north&quot;),
                  longitude:
                    make(&lt;longitude&gt;,
                         total-seconds: encode-total-seconds(77, 40, 24),
                         direction: #&quot;west&quot;))); 

  let sky = make(&lt;sky&gt;, inbound-aircraft: aircraft, airport: airport,
                 id: concatenate(&quot;over &quot;, airport.code));
  airport.sky-above := sky;
  runway.connected-to := vector(taxiway, sky);
  let taxiway-vector = vector(taxiway);
  for (gate in gates)
    gate.connected-to := taxiway-vector;
  end for;
  let runway-vector = vector(runway);
  taxiway.connected-to := concatenate(runway-vector, gates);
  sky.connected-to := runway-vector;
  airport;
end method build-simple-airport; 

define method test-airport () =&gt; (last-transition :: &lt;time-of-day&gt;)
  process-aircraft(build-simple-airport());
end method test-airport;</pre>
</td></tr></table>
<p>Now, we show the result of running <code>test-airport</code>:</p>
<pre>
<em>? </em>test-airport():
<em>Detailed aircraft schedule for Airport BLA
6:00: Flight KN 62 at Runway 11R-29L
6:02: Flight KN 62 at Taxiway Echo
6:02: Flight KN 7 at Runway 11R-29L
6:03: Flight KN 62 at Gate A1
6:04: Flight KN 7 at Taxiway Echo
6:05: Flight KN 7 at Gate A2
6:28: Flight KN 29 at Runway 11R-29L
6:30: Flight KN 29 at Taxiway Echo
6:37: Flight KN 62 at Taxiway Echo
6:37: Flight KN 29 at Gate A1
6:38: Flight KN 62 at Runway 11R-29L
6:39: Flight KN 7 at Taxiway Echo
6:42: Flight KN 62 at Sky over BLA
6:42: Flight KN 7 at Runway 11R-29L
6:46: Flight KN 7 at Sky over BLA
7:11: Flight KN 29 at Taxiway Echo
7:12: Flight KN 29 at Runway 11R-29L
7:16: Flight KN 29 at Sky over BLA
7:16: Flight KN 12 at Runway 11R-29L
7:18: Flight KN 12 at Taxiway Echo
7:18: Flight KN 18 at Runway 11R-29L</em>
<em>7:19: Flight KN 12 at Gate A1
7:20: Flight KN 18 at Taxiway Echo
7:20: Flight KN 44 at Runway 11R-29L
7:21: Flight KN 18 at Gate A2
7:22: Flight KN 44 at Taxiway Echo
7:53: Flight KN 12 at Taxiway Echo
7:53: Flight KN 44 at Gate A1
7:54: Flight KN 12 at Runway 11R-29L
7:55: Flight KN 18 at Taxiway Echo
7:58: Flight KN 12 at Sky over BLA
7:58: Flight KN 18 at Runway 11R-29L
8:02: Flight KN 18 at Sky over BLA
8:27: Flight KN 44 at Taxiway Echo
8:28: Flight KN 44 at Runway 11R-29L
8:32: Flight KN 44 at Sky over BLA
{class &lt;TIME-OF-DAY&gt;}</em>
</pre>

</div>

<div class="toc">


</div>

<div class="footer">
<address>Dylan Programming - 9 Apr 1999</address>
</div>
<div class="navigation">
	<p><a href="db_272.html" accesskey="n"><img src="next.gif" alt="next" /></a> <a href="db_270.html" accesskey="p"><img src="prev.gif" alt="previous" /></a> <a href="db_1.html" accesskey="t"><img src="top.gif" alt="top" /></a> <a href="db_2.html" accesskey="c"><img src="content.gif" alt="contents" /></a> <a href="db_349.html" accesskey="i"><img src="index.gif" alt="index" /></a></p>
</div>
<div class="copyright">
	<p>N Feinberg/S E Keene/R Mathews/P Tucker Withington, DYLAN PROGRAMMING, (c) 1997 Harlequin Inc. Reproduced by permission of Addison-Wesley Longman Publishing Company, Inc.  All rights reserved. No further copying, downloading or transmitting of this material is allowed without the prior written permission of the publisher.</p>
</div>
</body>
</html>
